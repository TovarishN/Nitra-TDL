using DotNet;
using DotNet.AstUtils;

using Nitra;
using Nitra.Declarations;

using System.Collections.Immutable;

namespace Tdl
{
  ignore-case declaration Suite : NamespaceMember
  {
    symbol
    {
      in Defs        : ImmutableArray[Def];
      in Platform    : PlatformSymbol;
      in Product     : ProductSymbol;
      in Statements  : ImmutableArray[SuiteStatement];
      in Type        : SuiteTypeSymbol;
      in EntityOpt    : ValueOption[EntitySymbol];
      in DefaultDefs : ImmutableArray[FormalParameterSymbol]; // Parameters of select deployments which not set explicitly.

      Kind           = "suite";
      SpanClass      = TdlLang.SuiteSpanClass;
      IsSameIdentity = candidate is Tdl.Suite;
    }

    out PlatformRef  : Ref[PlatformSymbol];
    out ProductRef   : Ref[ProductSymbol];
    out TypeRef      : Ref[DeclarationSymbol];
    out EntityRefOpt : ValueOption[Ref[EntitySymbol]];

    Platform.Scope              = Scope;
    PlatformRef                 = Platform.Ref.Resolve();
    Symbol.Platform             = PlatformRef.Symbol;

    Product.Scope               = Scope;
    ProductRef                  = Product.Ref.Resolve();
    Symbol.Product              = ProductRef.Symbol;

    Statements.Scope            = Scope;
    Statements.ScopeIn          = EmptyScope.Instance;
    Symbol.Statements           = Statements.Statement;

    Definitions.DefinitionScope = TdlUtils.MakeParameterScope(Statements.ScopeOut.UnionWith(Symbol.Type.Scope));
    Definitions.Scope           = context.GetExternalSymbolTable();
    Symbol.Defs                 = TdlUtils.MakeDefinitions(Definitions, context, Definitions.SymbolRef);

    TypeOpt.Scope               = Scope;
    TypeRef                     = TypeOpt.Ref.GetValueOrDefault();
    Symbol.Type                 = context.ResolveSuiteTypeSymbol(TypeRef);
    Symbol.DefaultDefs          = Name.MakeDefaultDefs(context, Symbol.Defs, Definitions.DefinitionScope, Symbol.Product);

    EntityOpt.Scope             = Scope;
    EntityRefOpt                = EntityOpt.Ref.ResolveOpt();
    Symbol.EntityOpt            = EntityRefOpt.GetSymbolOpt();

    SetUsed(Symbol.Statements);

    EntityOpt   : Reference?;
    TypeOpt     : Reference?;
    Platform    : Reference;
    Product     : Reference;
    Definitions : Definition*;
    Statements  : SuiteStatementAst*;
  }
}
