using DotNet;
using DotNet.AstUtils;

using Nitra;
using Nitra.Declarations;

using System;
using System.Collections.Immutable;

namespace Tdl
{
  abstract declaration Scenario : NamespaceMember, UsedDeclaration, TypedDeclaration
  {
    symbol
    {
    stage 3:
      in AggregateParametersScope : Scope;
      in AllScenarios             : ImmutableArray[ScenarioSymbol];

      Type = context.ToTdlContext().ScenarioType;
    }

    Symbol.AllScenarios = TdlUtils.CheckForRecursionScenario(self, context, Symbol, Symbol.AggregateParametersScope);

    unless (Symbol.IsUsed)
      Name.Warning(context, "Unused scenario.");

    | ignore-case Def : Parameterizable
      {
        symbol
        {
        stage 1:            
          in Actions        : ImmutableArray[ScenarioAction];
          in Deployment     : DeploymentSymbol;
          in Environments   : ImmutableArray[Environment];
          in ParallelSetOpt : Scenario.SetSymbol;
        stage 3:
          in Defs           : ImmutableArray[Def];
          in Filter         : object;

          Kind           = "scenario";
          SpanClass      = TdlLang.ScenarioSpanClass;
          IsSameIdentity = candidate is Tdl.Scenario;
        }
      stage 1:
        out DeploymentRef          : Ref[DeploymentSymbol];
        out MethodConditionalScope : ValueOption[Scope] = if (TdlTypingSettings.IsMethodTypingEnabled) VSome(Scope) else VNone();

        Parameters.DeclaredIn           = Symbol;
        Parameters.Scope                = Scope;
        Symbol.AggregateParametersScope = TdlUtils.MakeAggregateParametersScope(Symbol.ParameterScope, Symbol.Deployment.AggregateParametersScope, context);

        Actions.Scope                   = MethodConditionalScope.Value;
        Symbol.Actions                  = Actions.ScenarioActions;

        Deployment.Scope                = Scope;
        DeploymentRef                   = Deployment.Ref.Resolve();
        Symbol.Deployment               = DeploymentRef.Symbol;

        Environments.Scope              = Scope;
        Symbol.Environments             = Environments.Environment;

        Definitions.Scope               = context.GetExternalSymbolTable().UnionWith(Symbol.ParameterScope);
        Definitions.DefinitionScope     = context.GetScenarioParameterScope();
        Symbol.Defs                     = TdlUtils.MakeDefinitions(Definitions, Definitions.SymbolRef);

        FilterOpt.ScopeIn               = Definitions.Scope.UnionWith(Scope);
        FilterOpt.BuildInvocationScope  = context.BuildInvocationScope(Scope);
        FilterOpt.ExpectedType          = context.GetBooleanType();
        Symbol.Filter                   = if (FilterOpt.HasValue) FilterOpt.Value else null;

        ReportDuplicateBarrier(context, Symbol.Actions);

        Name.ChechRequired(context, "option", context.GetRequiredScenarioParameters(), Symbol.Defs);

        CheckOptionForDuplicates(context, Definitions.SymbolRef);

        Deployment   : Reference;
        Environments : EnvironmentAst*;
        Definitions  : Definition*;
        Actions      : Action*;
        FilterOpt    : DotNet.Expr?;
      }
    | ignore-case Set
      {
        symbol
        {
        stage 1:
          in Scenarios      : ImmutableArray[ScenarioSymbol];
          in IsMultiMachine : bool;
          in MultiMachineId : Guid;
          in BarriersCount  : list[BarrierSymbol * int];

          Kind           = "scenario set";
          SpanClass      = TdlLang.ScenarioSpanClass;
          IsSameIdentity = candidate is Tdl.Scenario;
        }

        Scenarios.Scope                 = Scope;
        Symbol.Scenarios                = TdlUtils.MakeScenarios(Scenarios.Ref);
        Symbol.AggregateParametersScope = TdlUtils.MakeAggregateParametersScope(Symbol.Scenarios, context);
        Symbol.MultiMachineId           = if (Symbol.IsMultiMachine) context.NewGuid() else Guid.Empty;
        Symbol.IsMultiMachine           = IsMultiMachine.HasValue && IsMultiMachine.Value;

        when (Symbol.IsMultiMachine)
          CheckMultiMachineScenario(context, Symbol.Scenarios, Symbol.AggregateParametersScope);

        CheckUniquenessOfScenarios(context, Symbol, Symbol.AggregateParametersScope);
        SetUsed(Symbol.AllScenarios, Symbol.IsUsed);

        IsMultiMachine : bool;
        Scenarios      : Reference*;
      }
    | ignore-case Generator : Container, Parameterizable
      {
        symbol
        {
          in Deployment : TypedDeclarationSymbol;
          in Method     : TypedDeclarationSymbol;
          in Platform   : TypedDeclarationSymbol;
          in Product    : TypedDeclarationSymbol;
        stage 3:
          in Defs       : ImmutableArray[Def];
          in Froms      : ImmutableArray[Code.From];
          in Filter     : object;

          Scope          = MemberTable;
          Kind           = "scenario generator";
          IsSameIdentity = candidate is Tdl.Scenario;
          SpanClass      = TdlLang.ScenarioSpanClass;
        }

      stage 1:
        out DeploymentRef : Ref[TypedDeclarationSymbol];
        out MethodRef     : Ref[TypedDeclarationSymbol];
        out PlatformRef   : Ref[TypedDeclarationSymbol];
        out ProductRef    : Ref[TypedDeclarationSymbol];

        Parameters.DeclaredIn           = Symbol;
        Parameters.Scope                = Scope;
        Froms.Scope                     = Scope;
        Froms.ContainingTable           = Symbol.MemberTable;
        Deployment.Scope                = Scope.HideWith(Symbol.MemberTable);
        Platform.Scope                  = Deployment.Scope;
        Product.Scope                   = Deployment.Scope;
        Method.Scope                    = Deployment.Scope;
        Definitions.DefinitionScope     = context.GetScenarioParameterScope();
        Definitions.Scope               = context.GetExternalSymbolTable().UnionWith(Symbol.ParameterScope);

        DeploymentRef                   = Deployment.Ref.Resolve();
        MethodRef                       = Method.Ref.Resolve();
        PlatformRef                     = Platform.Ref.Resolve();
        ProductRef                      = Product.Ref.Resolve();

        Symbol.Deployment               = DeploymentRef.Symbol;
        Symbol.Method                   = MethodRef    .Symbol;
        Symbol.Platform                 = PlatformRef  .Symbol;
        Symbol.Product                  = ProductRef   .Symbol;
        Symbol.Defs                     = TdlUtils.MakeDefinitions(Definitions, Definitions.SymbolRef);
        Symbol.Froms                    = await TdlUtils.MakeFroms(Froms);
        Symbol.AggregateParametersScope = await TdlUtils.MakeAggregateParametersScope(Symbol.Deployment, Symbol.Froms, context);

        FilterOpt.ScopeIn               = Definitions.Scope.UnionWith(Deployment.Scope);
        FilterOpt.BuildInvocationScope  = context.BuildInvocationScope(Scope);
        FilterOpt.ExpectedType          = context.GetBooleanType();
        Symbol.Filter                   = if (FilterOpt.HasValue) FilterOpt.Value else null;

        Deployment.CheckExpected(context, context.ToTdlContext().DeploymentType, DeploymentRef.Symbol.Type);
        Method.    CheckExpected(context, context.ToTdlContext().MethodType,     MethodRef.Symbol.Type);
        Platform.  CheckExpected(context, context.ToTdlContext().PlatformType,   PlatformRef.Symbol.Type);
        Product.   CheckExpected(context, context.ToTdlContext().ProductType,    ProductRef.Symbol.Type);

        Froms        : From*;
        Deployment   : Reference;
        Platform     : Reference;
        Product      : Reference;
        Definitions  : Definition*;
        Method       : QualifiedReference;
        FilterOpt    : DotNet.Expr?;
      }
  } // Scenario
}
