using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Tdl
{
  public variant Expr
  {
    | Error     { Location : Nitra.Location; }
    | String    { Value : string; }
    | True      { }
    | False     { }
    | Real      { Value : double; }
    | Integer   { Value : int; }
    | Reference { Name  : string; }

    public override ToString() : string
    {
      match (this)
      {
        | Error          => "<Error>"
        | String(value)  => "@\"" + value + '"'
        | True           => "true"
        | False          => "false"
        | Real      as x => x.Value.ToString()
        | Integer   as x => x.Value.ToString()
        | Reference as x => x.Name 
      }
    }
  }
}
