using Tdl;
using DotNet;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra;
using System;
using System.Collections.Generic;
using System.Linq;

namespace Tdl2Json
{
  public enum ResultKind
  {
    | Missing
    | NotSet
    | Value
    | ExternalParameter
    | GlobalParameter
    | External
    | Product
    | Error
  }

  module Expressions
  {
    public ToObject(this expression : Tdl.Expr,
                    values    : IReadOnlyDictionary[string, object]          = null,
                    variables : Dictionary[TypedDeclarationSymbol, Tdl.Expr] = null) : object
    {
      match (Convert(expression, values, variables))
      {
        | (_,     ResultKind.Missing)           => assert(false)
        | (value, ResultKind.NotSet)            => value
        | (value, ResultKind.Value)             => value
        | (value, ResultKind.ExternalParameter) => "#(" + value + ")"
        | (value, ResultKind.GlobalParameter)   => "$(" + value + ")"
        | (value, ResultKind.Product)           => value
        | (value, ResultKind.Error)             => throw NotSupportedException("An error has occurred when calculating '$expression': " + value)
        | (_,     ResultKind.External)          => throw NotSupportedException("Reference to externals root symbol is not allowed.")
      }
    }

    public Convert(expression : Tdl.Expr, values : IReadOnlyDictionary[string, object], variables : Dictionary[TypedDeclarationSymbol, Tdl.Expr]) : object * ResultKind
    {
      match (expression)
      {
        | Error              => ("<error>",             ResultKind.Value)
        | False              => (BooleanLiterals.False, ResultKind.Value)
        | True               => (BooleanLiterals.True,  ResultKind.Value)
        | Integer      as i  => (i.Value,               ResultKind.Value)
        | Real         as r  => (r.Value,               ResultKind.Value)
        | String       as s  => (s.Value,               ResultKind.Value)
        | List         as xs =>
          def elems = xs.Elems.Select(e => ToObject(e, values, variables));
          ($<#[..$elems]#>, ResultKind.Value)

        | Reference(Symbol=VarSymbol      as s)                                       => Convert(variables[s], values, variables)
        | Reference(Symbol=FieldSymbol    as s) when s.Owner?.Owner is ExternalSymbol => (s.Name, ResultKind.ExternalParameter)
        | Reference(Symbol=ExternalSymbol as s)                                       => (s.Name, ResultKind.External)
        | Reference(Symbol=TypedDeclarationSymbol(Type=ProductTypeSymbol) as s)       => (s.Name, ResultKind.Product)
        | Reference(Symbol=s) =>
          mutable referenceValue;
          if (values != null && values.TryGetValue(s.Name, out referenceValue))
            (referenceValue, ResultKind.Value)
          else
            (s.Name, ResultKind.GlobalParameter)

        | MemberAccess as m =>
          match (Convert(m.Expr, values, variables))
          {
            | (value, ResultKind.Product)  => ("Product:" + value + "." + m.Symbol.Name, ResultKind.GlobalParameter)
            | (_,     ResultKind.External) => (m.Symbol.Name,                            ResultKind.ExternalParameter)
            | _                            => throw NotSupportedException($"Invalid member access expression '$m'.")
          }

        | x => throw NotSupportedException($"Invalid expression '$x'.")
      }
    }

    public Calc(
      expression : Tdl.Expr,
      values     : IReadOnlyDictionary[string, Tdl.Expr],
      variables  : Dictionary[TypedDeclarationSymbol, Tdl.Expr],
      getPropertyValue : object * Declarations.DeclarationSymbol -> object * ResultKind
    ) : object * ResultKind
    {
      match (expression)
      {
        | Error              => (expression,            ResultKind.Error)
        | Tuple              => (expression,            ResultKind.Error)
        | NotSet             => (null,                  ResultKind.NotSet)
        | False              => (BooleanLiterals.False, ResultKind.Value)
        | True               => (BooleanLiterals.True,  ResultKind.Value)
        | Integer      as i  => (i.Value,               ResultKind.Value)
        | Real         as r  => (r.Value,               ResultKind.Value)
        | String       as s  => (s.Value,               ResultKind.Value)
        | Reference(Symbol=VarSymbol      as s)                                       => Calc(variables[s], values, variables, getPropertyValue)
        | Reference(Symbol=FieldSymbol    as s) when s.Owner?.Owner is ExternalSymbol => (s.Name, ResultKind.ExternalParameter)
        | Reference(Symbol=ExternalSymbol as s)                                       => (s.Name, ResultKind.External)
        | Reference(Symbol=TypedDeclarationSymbol(Type=ProductTypeSymbol) as s)       => (s,      ResultKind.Product)
        | Reference(Symbol=s) =>
          mutable referenceValue;
          if (values != null && values.TryGetValue(s.Name, out referenceValue))
            Calc(referenceValue, values, variables, getPropertyValue)
          else
            (s.Name, ResultKind.GlobalParameter)

        | List         as xs =>
          def elems = xs.Elems.Select(e => Calc(e, values, variables, getPropertyValue)).ToArray();
          (elems, ResultKind.Value)
        | MemberAccess as m =>
          match (Calc(m.Expr, values, variables, getPropertyValue))
          {
            | (value, ResultKind.Product)  => getPropertyValue(value, m.Symbol)
            | (_,     ResultKind.External) => (m.Symbol.Name, ResultKind.ExternalParameter)
            | _                            => throw NotSupportedException($"Invalid member access expression '$m'.")
          }
      }
    }

    public ToDictionary[TKey, TValue](this seq : Seq[TKey * TValue]) : Dictionary[TKey, TValue]
    {
      def dictionary = Dictionary();
      foreach ((k, v) in seq)
        dictionary.Add(k, v);
      dictionary
    }

    public ToNullable[T](this value : ParsedValue[T]) : T?
      where T: struct
    {
      if (value.HasValue) Nullable(value.Value) else null
    }
  }
}
