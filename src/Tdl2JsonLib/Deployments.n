using DotNet;

using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Utility.Pair;

using System;
using System.IO;
using System.Collections.Generic;
using System.Collections.Immutable;
using System.Text;
using System.Linq;

using Nemerle;
using Nemerle.Extensions;

using Nitra.Declarations;

using Tdl;
using QuickType;
using Tdl2Json.ExceptionHelper;

namespace Tdl2Json
{
  public module Deployments
  {
    private DeploymentScriptTemplate : string = ReadStringResource("Tdl2Json.DeploymentScriptTemplate.ps1");

    public Generate(symbols : IEnumerable[DeclarationSymbol], deploymentScriptHeader : string, deploymentToolPath : string) : Dictionary[string, QuickType.Deployment]
    {
      def result = Dictionary();
      // выбрасываем неиспользованный Empty-деплоймент
      foreach (d is DeploymentSymbol when !(d is Deployment.EmptySymbol) || (d.IsIsUsedEvaluated && d.IsUsed) in symbols)
        GenerateDeployment(d, deploymentScriptHeader, deploymentToolPath, result);
      result
    }

    public IsDeploymentCodeParameter(symbol : DotNet.TypedDeclarationSymbol) : bool
    {
      | s is DotNet.FormalParameterSymbol when s.IsDeclaredInEvaluated => s.DeclaredIn is Deployment.CodeSymbol
      | _ => false
    }

    private getParameterValue(parameter : DotNet.FormalParameterSymbol) : object
    {
      match (parameter.Default)
      {
        | Reference as r => "#(" + r.name + ")"
        | None           
        | String   
        | Bool     
        | Number   
        | Decimal  
        | Double   
        | Single => "$(" + parameter.Name + ")"
        | _              => throw NotSupportedException($"$(parameter.Default.GetType().Name) is not supported in deployment parameters")
      }
    }

    private getDefaultValue(parameter : DotNet.FormalParameterSymbol) : object
    {
      match (parameter.Default)
      {
        | String    as s => s.Value
        | Bool      as b when IsDeploymentCodeParameter(parameter) => if (b.Value) 1 else 0
        | Bool      as b => b.Value
        | Number    as n => n.Value
        | Decimal   as d => d.Value
        | Double    as d => d.Value
        | Single    as s => s.Value
        | None           => null
        | Reference as r => r.name
        | _              => throw NotSupportedException($"$(parameter.Default.GetType().Name) is not supported in deployment parameters")
      }
    }
    
    private GenerateDeploymentScript(script : Deployment.ScriptSymbol, result : Dictionary[string, QuickType.Deployment]) : void
    {
      def refDefs          = HashSet(script.Defs.Where(d => d.Expr is Expr.Reference).Select(d => (d.Expr :> Expr.Reference).Symbol.Name));
      def simpleParameters = script.Parameters.Where(p => !(p.Type is Tdl.ProductTypeSymbol)).ToArray();
      def parameters       = simpleParameters.Select(p => (p.Name, getParameterValue(p)));
      def defaultes        = script.Parameters.Select(p => (p.Name, getDefaultValue(p))).Where(((_, d)) => d != null).ToDictionary();
      def defs             = script.Defs.Select(d => (d.Name, d.Expr.ToObject(null)));
      def scriptArgs       = parameters.Where((p, _) => !refDefs.Contains(p)).Concat(defs).ToDictionary();

      def deploymentModel = Deployment() <- 
      {
        Type                    = TypeEnum.Script;
        ReturnValue             = if (script.Expected.HasValue)    script.Expected.Value  else null;
        RebootExitCode          = if (script.ForReboot.HasValue)   script.ForReboot.Value else null;
        Timeout                 = if (script.Timeout.HasValue)     script.Timeout.Value   else null;
        ScriptArgs              = scriptArgs;
        ScriptArgsDefaultValues = defaultes;
      };

      if (script.IsEmbedPathEvaluated)
      {
        def fileName                        = script.EmbedPath.Value;
        deploymentModel.ScriptData          = File.ReadAllBytes(fileName);
        deploymentModel.ScriptDataExtension = Path.GetExtension(fileName);
      }
      else
      {
        deploymentModel.ScriptPath          = if (script.IsPathEvaluated)      script.Path.Value      else null;
        deploymentModel.ScriptData          = if (script.IsBase64Evaluated)    script.Base64.Value    else null;
        deploymentModel.ScriptDataExtension = if (script.IsExtensionEvaluated) script.Extension.Value else null;
      }

      result.Add(script.Name, deploymentModel)
    }

    private GenerateDeploymentSelect(sel : Deployment.SelectSymbol, result : Dictionary[string, QuickType.Deployment]) : void
    {
      def separator   = "⁞";
      def deployments = sel.Deployments;
      def cases       = sel.Cases;
      assert2(deployments.Length == cases.Length);
      def condition   = string.Join(separator, sel.Parameters.Select(p => "$(" + p.Name + ")"));
      def values      = Dictionary();
      for (mutable i; i < deployments.Length; ++i)
      {
        def selector = string.Join(separator, cases[i].Select(v => if (v == null) JsonGenerator.ParameterDefaultValue else v));
        def seq      = deployments[i];
        mutable name;
        if (seq.Length == 1)
          name = seq[0].Name
        else
        {
          name = sel.Name + separator + selector;
          result.Add(name, Deployment() <- 
          { 
              Type        = TypeEnum.Sequence;
              Deployments = seq.Select(_.Name).ToArray()
          })
        }

        values.Add(selector, Dictionary() <- [ (name, object()) ])
      }

      result.Add(sel.Name, 
        Deployment() <- 
        { 
            Type      = TypeEnum.Select;
            Condition = condition;
            Values    = values;
        })
    }

    private GenerateDeploymentCurrying(currying : Deployment.CurryingSymbol, result : Dictionary[string, QuickType.Deployment]) : void
    {
      def deployments = Dictionary();
      deployments.Add(currying.BaseDeployment.Name, currying.ParameterValues.ToDictionary(p => p.Symbol.Name, p => p.Expr.ToObject(p.Symbol)));
      result.Add(currying.Name,
        Deployment() <-
        {
            Type        = TypeEnum.Sequence;
            Deployments = array[deployments];
        }
      )
    }

    private GenerateDeploymentCode(code : Deployment.CodeSymbol, deploymentScriptHeader : string, deploymentToolPath : string, result : Dictionary[string, QuickType.Deployment]) : void
    {
      def scriptArgs = Dictionary();
      def defaults   = Dictionary();

      def deploymentParameters = StringBuilder();
      def deploymentCode       = StringBuilder();
      def deploymentPrologue   = StringBuilder().AppendLine(deploymentScriptHeader);

      def getParameterReference(name) { "$_csharp_" + name }

      foreach (parameter in code.Parameters with index)
      {
        def parameterValue        = getParameterValue(parameter);
        def parameterDefaultValue = getDefaultValue(parameter);

        scriptArgs.Add(parameter.Name, parameterValue);
        when (parameterDefaultValue != null)
          defaults.Add(parameter.Name, parameterDefaultValue);

        when (index > 0)
          _ = deploymentParameters.AppendLine(",");

        def parameterName = "$" + parameter.Name;
        def parameterReference = getParameterReference(parameter.Name);

        def (type, value) =
          match (parameter.Type.FullName) // FIXMY: Какой трэш! Почему типы то не использовал?
          {
            | "System.String"  | "string" => ("string", $<#('@"' + $(parameterName).Replace('"', '""') + '"')#>)
            | "System.Boolean" | "bool"   => ("string", $<#switch($(parameterName)) { "0" { "false" } "false" { "false" } "" { "false" } $$null { "false" } default { "true" } }#>)
            | type                        => (type,     $<#$(parameterName).ToString("", [System.Globalization.CultureInfo]::InvariantCulture)#>)
          };

        _ = deploymentParameters.Append($"  [$(type)] $(parameterName)");
        _ = deploymentPrologue.AppendLine($<#$(parameterReference) = $(value)#>);
      }

      def ast = code.FirstDeclarationOrDefault :> Tdl.Deployment.Code;
      foreach (command in ast.Commands)
      {
        def commandVariable = "command" + command.StepNumberIn;
        def commandType     = command.TypeRef.Symbol.FullName;
        _ = deploymentCode.AppendLine($"var $(commandVariable) = __runtime__.CreateCommand<$(commandType)>();");

        foreach (argument is Tdl.CommmandCallInputArgument in command.Arguments)
        {
          def expr = argument.Value;
          def value =
            match (expr)
            {
              | DefExpr.String
              | DefExpr.True
              | DefExpr.False
              | DefExpr.RealLiteral
              | DefExpr.DecimalIntegerLiteral
              | DefExpr.HexadecimalIntegerLiteral => expr.ToString()
              | DefExpr.Reference as x =>
                match (x.Reference.Ref.Symbol)
                {
                  | FormalParameterSymbol            => getParameterReference(x.ToString())
                  | CommmandCallOutputArgumentSymbol => x.ToString()
                  | x => throw NotSupportedException($"Reference to '$(x)' is not supported in deployment code.")
                }

              | x => throw NotSupportedException($"Expression '$(x)' is not supported in deployment code.")
            };

          def parameterName = argument.ParameterName.Text;
          _ = deploymentCode.AppendLine($"$(commandVariable).$(parameterName) = $(value);");
        }

        _ = deploymentCode.AppendLine($"__runtime__.ExecuteCommand($(commandVariable));");

        foreach (argument is Tdl.CommmandCallOutputArgument in command.Arguments)
        {
          def parameterName = argument.ParameterName.Text;
          def variableName  = argument.Name.Text;
          _ = deploymentCode.AppendLine($"var $(variableName) = $(commandVariable).$(parameterName);");
        }
      }

      def deploymentScript = StringBuilder(DeploymentScriptTemplate)
        .Replace("{{Parameters}}", deploymentParameters.ToString())
        .Replace("{{Prologue}}",   deploymentPrologue.ToString())
        .Replace("{{Name}}",       code.Name)
        .Replace("{{Source}}",     deploymentCode.ToString())
        .Replace("{{Tool}}",       deploymentToolPath)
        .ToString();

      result.Add(code.Name,
        Deployment() <-
        {
          Type                    = TypeEnum.Script;
          ScriptPath              = null;
          ScriptData              = Encoding.UTF8.GetBytes(deploymentScript);
          ScriptDataExtension     = ".ps1";
          ReturnValue             = if (code.Expected.HasValue)  code.Expected.Value  else null;
          RebootExitCode          = if (code.ForReboot.HasValue) code.ForReboot.Value else null;
          Timeout                 = if (code.Timeout.HasValue)   code.Timeout.Value   else null;
          ScriptArgs              = scriptArgs;
          ScriptArgsDefaultValues = defaults;
        })
    }

    private GenerateDeployment(deploymentSymbol : DeploymentSymbol, deploymentScriptHeader : string, deploymentToolPath : string, result : Dictionary[string, QuickType.Deployment]) : void
    {
      match (deploymentSymbol)
      {
        | Deployment.ScriptSymbol   as script   => GenerateDeploymentScript(script, result)
        | Deployment.CurryingSymbol as currying => GenerateDeploymentCurrying(currying, result)
        | Deployment.CodeSymbol     as code     => GenerateDeploymentCode(code, deploymentScriptHeader, deploymentToolPath, result)
        | Deployment.SetSymbol as set =>
          def deployments = set.Deployments.Select(d => d.Name).ToArray();
          result.Add(set.Name, 
            Deployment() <- 
            { 
              Type        = TypeEnum.Sequence;
              Deployments = deployments;
            })

        | DeploymentRebootSymbol as reboot => 
          result.Add(reboot.Name, 
            Deployment() <-
            {
              Type          = TypeEnum.Reboot;
              RebootTimeout = if (reboot.Timeout.HasValue) reboot.Timeout.ValueOrDefault else "00:30:00";
            }
          )

        | Deployment.SelectSymbol as sel => GenerateDeploymentSelect(sel, result)
        | Deployment.EmptySymbol as empty => result.Add(empty.Name, Deployment() <- { Type = TypeEnum.Empty })
        | _ => _ = ThrowNotSupportedSymbol(deploymentSymbol) 
      }
    }

    private ReadStringResource(name : string) : string
    {
      using (stream = System.Reflection.Assembly.GetExecutingAssembly().GetManifestResourceStream(name))
      using (reader = System.IO.StreamReader(stream))
        reader.ReadToEnd();
    }
  } // module
} // namespace
